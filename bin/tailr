#!/usr/bin/env coffee

nomnom = require 'nomnom'
Tailr = require '../lib/tailr'
path = require 'path'

list = (value) -> value.split(',')

nomnom.colors()
  .script('tailr')

nomnom.command('list')
  .help('lists all logs in ~/.tailr.json')
  .callback ->
    tailr = new Tailr()
    logNames = (key for own key of tailr.getLogs())
    console.log logNames.join(' ')

nomnom.command('tail')
  .help("tails a given log file")
  .option('log',
    position: 1
    required: true
    help: 'the name of the logfile')
  .option('highlight',
    position: 2
    list: true
    help: 'words to highlight (e.g "word1 word2=blue word3=yellow")')
  .callback (options) ->
    tailr = new Tailr(wordsToHighlight: options.highlight)
    tailr.tail(options.log)

nomnom.command('completions')
  .help("shows the completion code to add to .zshrc")
  .callback ->
    dir = path.resolve(path.join(__dirname, '..', 'completions'))
    console.log "fpath=(#{dir} $fpath)"
    console.log "compinit"
  
if nomnom.parse()._.length == 0
  console.log nomnom.getUsage()


