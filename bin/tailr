#!/usr/bin/env coffee

commander = require 'commander'
Tailr = require '../lib/tailr'
path = require 'path'

commander.command('list')
  .description('lists all logs in ~/.tailr.json')
  .action ->
    tailr = new Tailr()
    logNames = (key for own key of tailr.getLogs())
    console.log logNames.join(' ')

commander.command('tail [log]')
  .description("tails a given log file")
  .action (log) ->
    if log?
      tailr = new Tailr()
      tailr.tail(log)
    else
      console.log "Missing 'log' argument, " +
        "run 'tailr list' to see available logs"

commander.command('completions')
  .description("shows the completion code to add to .zshrc")
  .action ->
    dir = path.resolve(path.join(__dirname, '..', 'completions'))
    console.log "fpath=(#{dir} $fpath)"
    console.log "compinit"
  
commander.parse(process.argv)
commander.help() if commander.args.length is 0
