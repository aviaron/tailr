#!/usr/bin/env coffee

commander = require 'commander'
Tailr = require '../lib/tailr'
path = require 'path'

list = (value) -> value.split(',')

commander.command('list')
  .description('lists all logs in ~/.tailr.json')
  .action ->
    tailr = new Tailr()
    logNames = (key for own key of tailr.getLogs())
    console.log logNames.join(' ')

commander.command('tail <log>')
  .description("tails a given log file")
  .option('-H, --highlight <string>',
    'Highlights the string in the output (e.g. "-H word1,word2=blue,word3=yellow"', list)
  .action (log, options) ->
    tailr = new Tailr(wordsToHighlight: options.highlight)
    tailr.tail(log)

commander.command('completions')
  .description("shows the completion code to add to .zshrc")
  .action ->
    dir = path.resolve(path.join(__dirname, '..', 'completions'))
    console.log "fpath=(#{dir} $fpath)"
    console.log "compinit"
  
commander.parse(process.argv)
commander.help() if commander.args.length is 0
